buildscript {
	repositories {
		jcenter()

		maven {
			url "http://files.minecraftforge.net/maven"
		}
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"

version = "1.12.2-1.1.2.0"
group = "com.therandomlabs.randomconfigs"
archivesBaseName = "randomconfigs"

sourceCompatibility = targetCompatibility = "1.8"

repositories {
	ivy {
		artifactPattern "https://endless.blue/files/ivy/[module]-[revision].[ext]"
	}
}

shadowJar {
	classifier = ""
	relocate "blue.endless.jankson", "${group}.repackage.blue.endless.jankson"
	configurations = [project.configurations.shadow]
}

dependencies {
	shadow "blue.endless:Jankson:0.0.4"
	compile "blue.endless:Jankson:0.0.4"
}

minecraft {
	version = "1.12.2-latest"
	runDir = "run"
	mappings = "stable_39"

	replace "@VERSION@", project.version
	replace "@FINGERPRINT@", project.findProperty("signSHA1")
}

processResources {
	//Ensure this task is redone when the versions change
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	//Replace version and mcversion in mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
}

jar {
	manifest {
		attributes "FMLAT": "randomconfigs_at.cfg"
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty("keyStore")
	}

	keyStore = project.findProperty("keyStore")
	alias = project.findProperty("keyStoreAlias")
	storePass = project.findProperty("keyStorePass")
	keyPass = project.findProperty("keyStoreKeyPass")
	inputFile = jar.archivePath
	outputFile = jar.archivePath
}

build.dependsOn signJar
