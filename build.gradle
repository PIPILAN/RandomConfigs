buildscript {
	repositories {
		jcenter()

		maven {
			url "https://www.dimdev.org/maven/"
		}

		maven {
			url "http://repo.spongepowered.org/maven"
		}
	}

	dependencies {
		classpath "org.dimdev:ForgeGradle:2.3-SNAPSHOT"
		classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
	}
}

apply plugin: "net.minecraftforge.gradle.tweaker-client"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"

version = "1.13-1.1.2.1-rift"
group = "com.therandomlabs.randomconfigs"
archivesBaseName = "randomconfigs"

sourceCompatibility = targetCompatibility = "1.8"

repositories {
	maven {
		url "https://www.dimdev.org/maven/"
	}

	ivy {
		artifactPattern "https://endless.blue/files/ivy/[module]-[revision].[ext]"
	}
}

dependencies {
	implementation "org.dimdev:rift:1.0.4-+:dev"
	shadow "blue.endless:Jankson:0.0.4"
	compile "blue.endless:Jankson:0.0.4"
}

shadowJar {
	classifier = ""
	relocate "blue.endless.jankson", "com.therandomlabs.randomconfigs.repackage.blue.endless.jankson"
	configurations = [project.configurations.shadow]
}

reobf {
	shadowJar {
		mappingType = "NOTCH"
	}
}

tasks.build.dependsOn reobfShadowJar

artifacts {
	archives shadowJar
}

mixin {
	defaultObfuscationEnv notch
	add sourceSets.main, "mixins.randomconfigs.refmap.json"
}

minecraft {
	version = "1.13"
	runDir = "run"
	mappings = "snapshot_20180908"
	tweakClass = "org.dimdev.riftloader.launch.RiftLoaderClientTweaker"
}
